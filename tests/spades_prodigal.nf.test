nextflow_pipeline {

    name "Test pipeline"
    script "../main.nf"
    tag "pipeline"
    profile "test_spades_prodigal"

    test("-profile test_spades_prodigal") {

        when {
            params {
                outdir = "$outputDir"
            }
        }

        then {
            // stable_name: All files + folders in ${params.outdir}/ with a stable name
            def stable_name = getAllFilesFromDir(params.outdir, relative: true, includeDir: true, ignore: ['pipeline_info/*.{html,json,txt}'])
            // stable_path: All files in ${params.outdir}/ with stable content
            def stable_path = getAllFilesFromDir(params.outdir, ignoreFile: 'tests/.nftignore')
            assertAll(
                { assert workflow.success },
                { assert snapshot(
                    // Number of successful tasks
                    workflow.trace.succeeded().size(),
                    // pipeline versions.yml file for multiqc from which Nextflow version is removed because we test pipelines on multiple Nextflow versions
                    removeNextflowVersion("$outputDir/pipeline_info/nf_core_metatdenovo_software_mqc_versions.yml"),
                    // All stable path name, with a relative path
                    stable_name,
                    // All files with stable contents
                    stable_path,
                    // Important files with unstable content
                    path("${params.outdir}/samtools/spades.SAMPLE1_PE.flagstat").readLines().size(),
                    path("${params.outdir}/summary_tables/spades.prodigal.counts.tsv.gz").linesGzip.findAll { l -> l.contains('orf') },
                    path("${params.outdir}/summary_tables/spades.prodigal.overall_stats.tsv.gz").linesGzip.size(),
                    path("${params.outdir}/summary_tables/spades.prodigal.overall_stats.tsv.gz").linesGzip.findAll { l -> l.contains('n_feature_count') },
                ).match() },
                { assert path("${params.outdir}/spades/spades.spades.log").readLines().any { l -> l.contains('======= SPAdes pipeline finished.') } },
                { assert path("${params.outdir}/spades/spades.assembly.gfa.gz").linesGzip.size() > 10000 },
                { assert path("${params.outdir}/spades/spades.transcripts.fa.gz").linesGzip.size() > 3500 },
                { assert path("${params.outdir}/samtools/spades.SAMPLE1_PE.idxstats").readLines().size() > 1700 },
                { assert path("${params.outdir}/prodigal/spades.prodigal.gff.gz").linesGzip.findAll { l -> l.contains('CDS') }.size() > 3000 },
                { assert path("${params.outdir}/summary_tables/spades.prodigal.counts.tsv.gz").linesGzip.size() > 3000 },
            )
        }
    }
}
